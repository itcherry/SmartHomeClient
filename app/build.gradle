plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'kotlin-android'
}

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

ext {
    support_version = '28.0.0'
    retrofit_version = '2.9.0'
    rxBindingKotlinVersion = '2.2.0'
    dagger_version = '2.15'
    glide_version = "4.10.0"
    firebase_version = "16.0.4"
    constraintLayoutVersion = '1.1.3'
    leakCanaryVersion = '2.12'
}

def CONFIG = { k -> "\"${project.properties.get(k)}\"" }

android.buildFeatures.buildConfig true

android {
    compileSdkVersion 34
    namespace "com.chernysh.smarthome"
    defaultConfig {
        applicationId "com.chernysh.smarthome"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", "SmartDynasty-v" + versionName);
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", 'HOST_LINK_GLOBAL', '\"http://95.67.92.25\"')
        buildConfigField("String", 'HOST_LINK_LOCAL', '\"http://192.168.88.253\"')
        buildConfigField("String", 'PORT_API', '\":8080/\"')
        buildConfigField("String", 'PORT_SOCKET', '\"9092\"')
        buildConfigField("String", 'RTSP_URL', CONFIG("SMART_HOME_RTSP"))
        buildConfigField("String", 'PIN', CONFIG("PIN"))
    }
    viewBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/notice'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/license'
    }
    lintOptions {
        abortOnError false
        enable 'BackButton'
        enable 'GradleDynamicVersion'
        enable 'NewerVersionAvailable'

        disable 'MissingTranslation'
        disable 'InvalidPackage'
        disable 'ExtraTranslation'
    }
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }
    kotlin {
        jvmToolchain(17)
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //MVI
    implementation 'com.hannesdorfmann.mosby3:mvi:3.1.1'

    //Security
    implementation 'androidx.biometric:biometric:1.1.0'

    //Android
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Networking
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.1'
    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.3m'

    //RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxBindingKotlinVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxBindingKotlinVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding:$rxBindingKotlinVersion"

    //Dagger 2
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    //Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    //Firebase
    implementation "com.google.firebase:firebase-messaging:23.3.0"

    //Sockets
    implementation project(":kotlinrxsocketsclient-release")
    implementation('io.socket:socket.io-client:0.9.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    //Utils
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.tspoon.traceur:traceur:1.0.1'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    debugImplementation "com.github.chuckerteam.chucker:library:4.0.0"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:4.0.0"

    // View
    implementation 'com.balysv:material-ripple:1.0.2'

    // Video
    implementation 'de.mrmaffen:libvlc-android:2.1.12@aar'
}